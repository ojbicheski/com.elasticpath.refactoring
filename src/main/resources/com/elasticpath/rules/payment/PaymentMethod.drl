package com.elasticpath.rules.payment

import java.util.ArrayList;
import java.util.List;
import java.util.Locale;

import com.elasticpath.refactoring.PaymentMethodDetails
import com.elasticpath.refactoring.AuthorizationService

import com.elasticpath.refactoring.enums.FulfillmentMessages;
import com.elasticpath.refactoring.enums.FulfillmentPermissions;
import com.elasticpath.refactoring.enums.OrderStatus;
import com.elasticpath.refactoring.enums.PaymentAttributeKey;

import com.elasticpath.refactoring.util.Localized

import com.elasticpath.rules.Result;

rule "GOOGLE_CHECKOUT"

dialect "java"

when
	$paymentMethodDetails: 
		PaymentMethodDetails(
			paymentType == "GOOGLE_CHECKOUT"
		)
	
	$result : Result()
	$locale : Locale()
then
	System.out.println("GOOGLE_CHECKOUT");
	
	$result.set(
		Localized.resolveLocalizedString(
			FulfillmentMessages.GOOGLE, 
			$locale, 
			$paymentMethodDetails.getAttributeAsString(
				PaymentAttributeKey.SHIPMENT_NUMBER), 
			$paymentMethodDetails.getAttributeAsString(
				PaymentAttributeKey.EMAIL)
		));
end

rule "GIFT_CERTIFICATE"

dialect "java"

when
	$paymentMethodDetails : 
		PaymentMethodDetails(
			paymentType == "GIFT_CERTIFICATE"
		)

	$result : Result()
	$authorizationService : AuthorizationService()
	$locale : Locale()
then
	System.out.println("GIFT_CERTIFICATE");
	
	// This determines if the user is authorized to see the full gift certificate number or just a masked version
	if ($authorizationService.isAuthorized(FulfillmentPermissions.VIEW_FULL_CREDITCARD_NUMBER)) {
		$result.set(
			Localized.resolveLocalizedString(
				FulfillmentMessages.GIFT_CERTIFICATE, 
				$locale, 
				$paymentMethodDetails.getAttributeAsString(
					PaymentAttributeKey.GIFT_CERTIFICATE_CODE)));
	} else {
		$result.set(
			Localized.resolveLocalizedString(
				FulfillmentMessages.GIFT_CERTIFICATE, 
				$locale, 
				$paymentMethodDetails.getAttributeAsString(
					PaymentAttributeKey.MASKED_GIFT_CERTIFICATE_CODE)));
	}

	update($result)
end

rule "PAYPAL_EXPRESS"

dialect "java"

when
	$paymentMethodDetails : 
		PaymentMethodDetails(
			paymentType == "PAYPAL_EXPRESS"
		)

	$result : Result()
then
	System.out.println("PAYPAL_EXPRESS");

	$result.set(
		$paymentMethodDetails.getAttributeAsString(
			PaymentAttributeKey.EMAIL));

	update($result)
end

rule "RETURN_AND_EXCHANGE"

dialect "java"

when
	$paymentMethodDetails : 
		PaymentMethodDetails(
			paymentType == "RETURN_AND_EXCHANGE"
		)

	$result : Result()
	$locale : Locale()
then
	System.out.println("RETURN_AND_EXCHANGE");

	if ($paymentMethodDetails.getOrderStatus() == OrderStatus.AWAITING_EXCHANGE) {
		$result.set(
			Localized.resolveLocalizedString(
				FulfillmentMessages.EXCHANGE_PENDING, 
				$locale));
	} else if ($paymentMethodDetails.getOrderStatus() != OrderStatus.AWAITING_EXCHANGE && 
				$paymentMethodDetails.getOrderStatus() != OrderStatus.CANCELLED) {
		$result.set(
			Localized.resolveLocalizedString(
				FulfillmentMessages.EXCHANGE_COMPLETED, 
				$locale));
	}

	update($result)
end

rule "DEFAULT"

dialect "java" 

when
	$paymentMethodDetails : 
		PaymentMethodDetails(
			paymentType != "GOOGLE_CHECKOUT" &&
			paymentType != "GIFT_CERTIFICATE" &&
			paymentType != "PAYPAL_EXPRESS" &&
			paymentType != "RETURN_AND_EXCHANGE"
		)

	$result : Result()
	$locale : Locale()
	$authorizationService : AuthorizationService()
then
	System.out.println("DEFAULT");

	List<String> bindings = new ArrayList<>();
	bindings.add(
		$paymentMethodDetails.getAttributeAsString(
			PaymentAttributeKey.CARD_TYPE));

	// This determines if the user is authorized to see the full credit card number or just a masked version
	if ($authorizationService.isAuthorized(FulfillmentPermissions.VIEW_FULL_CREDITCARD_NUMBER)) {
		bindings.add(
			$paymentMethodDetails.getAttributeAsString(
				PaymentAttributeKey.PAN));
	} else {
		bindings.add(
			$paymentMethodDetails.getAttributeAsString(
				PaymentAttributeKey.MASKED_PAN));
	}
	bindings.add(
		$paymentMethodDetails.getAttributeAsString(
			PaymentAttributeKey.EXPIRY_MONTH));
	bindings.add(
		$paymentMethodDetails.getAttributeAsString(
			PaymentAttributeKey.EXPIRY_YEAR));

	$result.set(
		Localized.resolveLocalizedString(
			FulfillmentMessages.CARD_DESCRIPTION, 
			$locale, 
			bindings.toArray()));

	update($result)
end
